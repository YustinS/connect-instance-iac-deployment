# variables.tf
############
# Required
############

variable "name_prefix" {
  description = "String that will be prepended to names for consistency. Contents only [a-zA-Z0-9-]"
  type        = string
  validation {
    condition     = can(regex("^[a-zA-Z0-9-]+$", var.name_prefix))
    error_message = "Please ensure that 'name_prefix' uses only the expected characters [a-zA-Z0-9-]."
  }
}

variable "connect_identity_management_type" {
  description = "What Identity Management that Connect will use. Valid values are 'SAML', 'CONNECT_MANAGED', or 'EXISTING_DIRECTORY'. 'SAML' or 'EXISTING_DIRECTORY' will require more resources to be created/managed externally to this module. See https://docs.aws.amazon.com/connect/latest/adminguide/connect-identity-management.html for more information"
  type        = string
  validation {
    condition     = can(regex("^(SAML|CONNECT_MANAGED|EXISTING_DIRECTORY)$", var.connect_identity_management_type))
    error_message = "Please ensure that 'connect_identity_management_type' is one of the expected inputs. Valid values are 'SAML', 'CONNECT_MANAGED', or 'EXISTING_DIRECTORY'."
  }
}

variable "environment" {
  description = "The environment this is running against, such as 'dev', 'test' etc. Will the postfixed to names for consistency."
  type        = string
}

###############
# Optionals
###############

variable "existing_s3_bucket_name" {
  description = "If desired an existing S3 Bucket can be used to store content generated by Connect. If not provided, a new S3 Bucket will be created. The prefix for the bucket will be 'connect/{instance_name}/{content_prefix}'"
  type        = string
  default     = null
}

variable "connect_existing_directory_id" {
  description = "If variable `connect_identity_management_type` is set to 'EXISTING_DIRECTORY', then the ID of the Existing Directory Connect will be joined to"
  type        = string
  default     = "N/A"
}

variable "log_encryption_cmk" {
  description = "KMS ARN for a key used to encrypt created CloudWatch Log Group(s). Ensure KMS has the appropriate Key Policy"
  type        = string
  default     = null
}

variable "s3_encryption_cmk" {
  description = "KMS ARN for a key used to encrypt created S3 Content created from Connect. Ensure KMS has the appropriate key policy, and that Connect or its Service Role has appropriate permissions to encrypt content"
  type        = string
  default     = null
}

variable "log_retention_days" {
  description = "The length of time to maintain logs for"
  type        = number
  default     = 90
  validation {
    condition     = contains([1, 3, 5, 7, 14, 30, 60, 90, 120, 150, 180, 365, 400, 545, 731, 1827, 3653, 0], var.log_retention_days)
    error_message = "Please ensure that 'log_retention_days' is a valid value as accepted by AWS CloudWatch."
  }
}

variable "inbound_calls_enabled" {
  description = "Will the created Connect Instance be able to receive inbound calls"
  type        = bool
  default     = true
}

variable "outbound_calls_enabled" {
  description = "Will the created Connect Instance be able to make outbound calls"
  type        = bool
  default     = true
}

variable "early_media_enabled" {
  description = "Will Early Media be heard by the Agent when a call is answered. Recommended left on unless specific reasons are identified"
  type        = bool
  default     = true
}

variable "auto_resolve_best_voices_enabled" {
  description = "Will Connect attempt to use the best avilaible voice. Recommended left on unless specific reasons are identified"
  type        = bool
  default     = true
}

variable "contact_flow_logs_enabled" {
  description = "Will Connect log Contact Flow activity to CloudWatch logs (if the appropriate setting is enabled in the Contact Flow). Recommended left on to assist with trouble shooting, and disabling logging in specific flows as required"
  type        = bool
  default     = true
}

variable "contact_lens_enabled" {
  description = "Will Connect use Amazon Connect Contact Lens to analyse calls. For proper usage ensure instance storage configuration has been configured, and the appropriate flow blocks configure the usage inside of a contact flow"
  type        = bool
  default     = true
}

variable "multi_party_conference_enabled" {
  description = "Will Multiparty conferencing be enabled for usage in the instance. For more information see https://docs.aws.amazon.com/connect/latest/adminguide/multi-party-calls.html"
  type        = bool
  default     = false
}

variable "tags" {
  description = "A map of tags to assign to resources. These will be specific tags for this module, otherwise it is recommended to use `default_tags` in the AWS Provider"
  type        = map(any)
  default     = {}
}
